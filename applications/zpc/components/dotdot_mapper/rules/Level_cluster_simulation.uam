
// Multilevel Switch CC defines
def zwMULTILEVEL_STATE     0x2602
// Z-Wave Color switch
def zwCOLOR_STATE            0x3303

// Level Cluster
def zbLEVEL_CLUSTER_LEVEL   0x00080000
def zbLEVEL_CLUSTER_REMAINING_TIME   0x00080001
def zbLEVEL_CLUSTER_TRANSITION_TIME   0x00080010
def zbLEVEL_CLUSTER_MIN_LEVEL 0x00080002
def zbLEVEL_CLUSTER_MAX_LEVEL 0x00080003

// Constants
def min_level 0
def max_level 255

// Capabilities
def zwave_color_exists (e'zwCOLOR_STATE)
def no_multilevel_switch (e'zwMULTILEVEL_STATE == 0)

// We simulate Level if there is no Multilevel Switch, Color Switch is present
def simulate_level (no_multilevel_switch & zwave_color_exists)

scope 0 chain_reaction(1) clear_desired(0) {

  // Constants.
  r'zbLEVEL_CLUSTER_MIN_LEVEL = if (simulate_level) min_level undefined
  r'zbLEVEL_CLUSTER_MAX_LEVEL = if (simulate_level) max_level undefined

  // Validate the desired value when it gets updated
  d'zbLEVEL_CLUSTER_LEVEL =
    if (simulate_level == 0) undefined
    if (d'zbLEVEL_CLUSTER_LEVEL < min_level) min_level
    if (d'zbLEVEL_CLUSTER_LEVEL > max_level) max_level
    undefined

  // If simulating, desired value just propagates to the Reported value.
  r'zbLEVEL_CLUSTER_LEVEL =
    if (simulate_level == 0) undefined
    if (d'zbLEVEL_CLUSTER_LEVEL < min_level) min_level
    if (d'zbLEVEL_CLUSTER_LEVEL > max_level) max_level
    (d'zbLEVEL_CLUSTER_LEVEL or max_level)

  // Timing not available if simulating, we would need the mapper to start transitions:
  r'zbLEVEL_CLUSTER_REMAINING_TIME =
    if (simulate_level == 0) undefined
    (d'zbLEVEL_CLUSTER_REMAINING_TIME or 0)

  r'zbLEVEL_CLUSTER_TRANSITION_TIME = if (simulate_level) 0 undefined
  d'zbLEVEL_CLUSTER_TRANSITION_TIME = if (simulate_level) 0 undefined
}
